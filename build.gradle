plugins {
    id 'java'
    id 'maven-publish'
    id "com.diffplug.spotless" version "8.1.0"

    id "io.freefair.lombok" version "9.1.0" apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'org.springframework.boot' version '3.2.0' apply false
}

ext {
    set('testcontainers.version', '2.0.3')
}

spotless {
    groovyGradle {
        target '*.gradle'
        greclipse()
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }

    java {
        eclipse().configFile('projects/config/eclipse-formatter-spring.xml')
    }
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: "com.diffplug.spotless"
    apply plugin: "io.freefair.lombok"
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'

        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            gpr(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/mito99/git-practice")
                credentials {
                    username = project.findProperty('gpr.user') ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty('gpr.key') ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
